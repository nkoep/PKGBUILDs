# Maintainer: Niklas Koep <niklas dot koep at gmail dot com>
pkgname=libjit-git
pkgver=r585.a8293e1
pkgrel=1
pkgdesc="Library for generic Just-in-Time compiler functionality"
arch=("i686" "x86_64")
url="https://www.gnu.org/software/libjit/"
license=("LGPL")
makedepends=("git")
provides=("libjit")
conflicts=("libjit")
options=("!libtool")
source=("$pkgname"::"git://git.savannah.gnu.org/libjit.git")
md5sums=("SKIP")

pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" \
        "$(git rev-parse --short HEAD)"
}

build() {
    cd ${pkgname}
    msg "Starting make..."
    ./auto_gen.sh
    ./configure --prefix=/usr
    make
}

package() {
    cd ${pkgname}
    make DESTDIR="${pkgdir}" install
}

